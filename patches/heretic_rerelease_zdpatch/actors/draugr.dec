actor Draugr 3101 {
    Game Heretic
    Monster

    ReactionTime 12
    Radius 16
    Height 62
    Speed 10
    Health 60
    PainChance 192
    Mass 100
    SeeSound "mum2sit"
    ActiveSound "mum2sit"
    PainSound "mum2pai"
    DeathSound "mum2dth"

    States {
        Spawn:
            MUM2 AB 10 A_Look
            Loop
        See:
            MUM2 AABBCCDD 2 A_Chase
            Loop
        Missile:
            MUM2 E 5 A_FaceTarget
            MUM2 F 5 bright A_FaceTarget
            MUM2 G 10 bright A_SpawnProjectile("DraugrFX1")
            goto See
        Pain:
            MUM2 E 2
            MUM2 E 2 A_Pain
            goto See
        Death:
            MUM2 H 6
            MUM2 I 6 A_Scream
            MUM2 JK 6
            MUM2 L 6 A_NoBlocking
            MUM2 M -1
            Stop
    }
}

actor DraugrFX1 {
    Projectile

    Radius 8
    Height 14
    Speed 14
    Damage 2
    Mass 100
    SeeSound "mum2atk"
    DeathSound "maghit"

    States {
        Spawn:
            PSKL ABCB 5 bright
            Loop
        Death:
            PSKL DEFG 5 bright
            Stop
    }
}
